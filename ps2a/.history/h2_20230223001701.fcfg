## Natural Language Toolkit: h3b.fcfg
##
## Second base grammar for sentences about strings
## It adds the verb 'follows'.
##
## Author: Mats Rooth
##
## For license information, see LICENSE.TXT

% start S
############################
# Grammar Rules
#############################
S[SEM=<?subj(?vp)>] -> DP[SEM=?subj] VP[SEM=?vp]

S[SEM=<?vp(?subj)>] -> NP[SEM=?subj] VP[SEM=?vp]

DP[ SEM=<?X(?P)>] -> Det[SEM=?X] N[SEM=?P] 

VP[SEM=?Q] -> 'is' A[SEM=?Q]

# This is included for testing.
VP[SEM=<\x.offend(x)>] -> 'offends'

# Transitive verb with individual object.
VP[ SEM=<?R(?n)>] -> TV[SEM=?R] NP[SEM=?n]

# Transitive verb with quantifier object.
# The object is given minimal scope.
VP[ SEM=<\m.?X(\n.(?R(n)(m)))>] -> TV[SEM=?R] DP[SEM=?X]

#############################
# Lexical Rules
#############################

A[SEM=<\n.exists c.(vowel(c) & char(n,c))>] -> 'vocalic'

Det[SEM=<\P Q.all n.(P(n) -> Q(n))>] -> 'every'
Det[SEM=<\P Q.exists n.(P(n) & Q(n))>] -> 'a'
Det[SEM=<\P Q.exists n.(P(n) & Q(n))>] -> 'some'

N[SEM=<\n.char(n,leti)>] -> 'i'
N[SEM=<\n.char(n,lete)>] -> 'e'
N[SEM=<\n.char(n,letu)>] -> 'u'

N[SEM=<\n.char(n,letp)>] -> 'p'
N[SEM=<\n.char(n,lett)>] -> 't'
N[SEM=<\n.char(n,letk)>] -> 'k'

N[SEM=<\n.exists c.char(n,c)>] -> 'letter'

NP[SEM=<1>] -> 'letter' 'one'
NP[SEM=<2>] -> 'letter' 'two'
NP[SEM=<3>] -> 'letter' 'three'
NP[SEM=<4>] -> 'letter' 'four'
NP[SEM=<5>] -> 'letter' 'five'

TV[SEM=<\m n.le(m,n)>] -> 'follows'










